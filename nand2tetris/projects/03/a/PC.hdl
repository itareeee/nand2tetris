// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // 11:reset, 10:load, 01:inc, 00:prev
    Or(a=reset, b=load, out=resetOrLoad);         // 1x

    Not(in=load, out=notLoad); 
    And(a=inc, b=notLoad, out=incNotLoad);
    Or(a=reset, b=incNotLoad, out=resetOrInc); // x1

    Add16(a=prev, b[0]=true, out=incremented);
    Mux4Way16(a=prev, b=incremented, c=in, d=false, sel[1]=resetOrLoad, sel[0]=resetOrInc, out=next);

    Register(in=next, load=true, out=prev, out=out);
}

